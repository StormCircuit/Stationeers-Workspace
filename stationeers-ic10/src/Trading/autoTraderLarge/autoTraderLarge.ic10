alias solver d0
alias traderFilter d1
alias padSize d4
alias antennaDish d2
alias resetButton d3
alias largeDish d4
alias currFilter r4
alias prevSignal r6
define topOfStack 12 #change this to how many filters you have (if starting at 0, +1)
define minWattsContact 200 #100 and above are far traders. Far (6x6+) need a large dish
move sp topOfStack
s db Setting 0
findContact:
s antennaDish BestContactFilter -1 # ==SCANNING ROUTINE==
s largeDish BestContactFilter -1
s antennaDish Setting 3000
s largeDish Setting 3000
jal moveScannerDish
jal setFilter #let user change filters only while sweeping
l r0 antennaDish ContactTypeId #check contactTypeID
bne r0 currFilter findContact
s largeDish Setting 50000
l r0 antennaDish Horizontal #move large dish to medium dish location
s largeDish Horizontal r0
l r0 antennaDish Vertical
s largeDish Vertical r0
l r0 antennaDish ContactTypeId #check contactTypeID
bne r0 currFilter findContact
l r0 largeDish MinimumWattsToContact #check trader tier
yield
breq r0 -1 -4 #wait for min wattage to show up
blt r0 minWattsContact findContact
l r0 antennaDish SignalID
beq r0 prevSignal findContact #avoid re-contacting same trader
l r0 antennaDish SignalID#if the checks pass, proceed to contact routine
s largeDish BestContactFilter r0  #set BestContactFilter to our signal ID we found
l r0 largeDish SignalStrength #wait for green bar
yield
breq r0 -1 -2
l r0 largeDish MinimumWattsToContact
l r1 largeDish WattsReachingContact
brgt r1 r0 3 #if we already are reaching it, dont solve
s largeDish Activate 0
jal solverRoutine
s largeDish Activate 1
l r0 largeDish SignalID
beq r0 -1 findContact #if it's -1 then the signal was lost
l r0 largeDish MinimumWattsToContact
l r1 largeDish WattsReachingContact
brlt r1 r0 -7 #if solver failed, retry
l r0 largeDish InterrogationProgress
brne r0 1 -4 #if not 1, wait
s largeDish Setting 3000
s db Setting 1 #trigger waiting flag on housing, set to 0 to continue
waitForReset:
bdns resetButton noButton
l r0 resetButton Setting #give user option to reset with button
brne r0 1 2
s db Setting 0
noButton:
l r0 antennaDish SignalID
brne r0 -1 2 #signal lost
s db Setting 0
l r0 db Setting
bne r0 0 waitForReset #wait for housing reset
l r0 antennaDish SignalID
move prevSignal r0 #record previous signal
j findContact

setFilter:
l currFilter traderFilter Setting #filters are loaded onto stack, dial must be 0-12
get currFilter db currFilter
j ra
moveScannerDish:
l r1 antennaDish Horizontal #sweep dish, check filter
add r0 r1 11
s antennaDish Horizontal r0
mul r0 r1 0.01
sin r0 r0
mul r0 r0 45
add r0 r0 45
s antennaDish Vertical r0
l r0 antennaDish Idle
yield
brne r0 1 -2 #wait for dish to idle
yield
j ra
solverRoutine:
move r13 0
move r15 ra #store ra in r15
loadSolver:
l r0 largeDish SignalID
beq r0 -1 findContact #if signal lost, restart
l r0 largeDish Idle
yield
breq r0 0 -4 #wait for dish to idle
yield
l r1 largeDish Horizontal
put solver r13 r1
add r13 r13 1
l r1 largeDish Vertical
put solver r13 r1
add r13 r13 1
l r1 largeDish SignalStrength
put solver r13 r1
add r13 r13 1
breq r13 9 5 #r0 = 9 means solver is filled, branch
l r0 largeDish Horizontal
add r0 r0 2
s largeDish Horizontal r0 #move dish 2 degrees
j loadSolver
s solver Setting 1
l r0 solver Setting
yield
breq r0 1 -2 #wait for solver Setting to = 0
get r0 solver 0
s largeDish Horizontal r0
get r0 solver 1
s largeDish Vertical r0
l r0 largeDish Idle
yield
brne r0 1 -2 #wait for dish to idle
yield
j r15 #use stored ra