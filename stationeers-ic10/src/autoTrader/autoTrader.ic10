alias solver d0
alias selectionDial d1
alias antenna d2
alias resetButton d3
alias currFilter r4
alias wattsReaching r5
alias wattsNeeded r6
alias currSigID r7
define topOfStack 12 #change this to how many filters you have (if starting at 0, +1)
define minWattsContact 180 #180 and above are medium traders. Large+ is 6,000+

move sp topOfStack

findContact:
jal setFilter #let user change filters only while sweeping
jal moveDish
l r0 antenna ContactTypeId #check contactTypeID
bne r0 currFilter continueSweep
l r0 antenna MinimumWattsToContact #check trader tier
blt r0 minWattsContact continueSweep
l r0 antenna SignalID
jal checkIgnoredSignalID #check ignore list
beq r0 1 continueSweep
j attemptContact #if the checks pass, proceed to contact routine
continueSweep:
j findContact


attemptContact:
jal solverRoutine #run solver routine to get close/at target
l r0 antenna MinimumWattsToContact #wait for watts reaching to hit min
l r1 antenna WattsReachingContact
yield
brlt r1 r0 -3
s antenna Activate 1 #watts min reached, activate dish and wait for progress = 100%
l r0 antenna InterrogationProgress
yield
brne r0 1 -2
waitForReset:
s db Setting 1
bdns resetButton noButton
l r0 resetButton Setting
breq r0 1 2
s db Setting 0
noButton:
l r0 db Setting
bne r0 0 waitForReset #wait for housing reset
l r0 antenna SignalID 
push r0
s antenna Vertical 45
j findContact

checkIgnoredSignalID:
move sp topOfStack
move currSigID r0
move r0 0
ignoreCheckLoop:
add sp sp 1
pop r0
brne currSigID r0 3 #if currSigID matches an ignored signal, set r0 and branch
move r0 1
j ra
beq r0 0 ra #if stack entry empty (meaning end of stack), branch
j ignoreCheckLoop

moveDish:
l r0 antenna Horizontal #sweep dish, check filter
add r0 r0 1
s antenna Horizontal r0
l r0 antenna Vertical
mod r1 r0 2
brne r1 0 3
s antenna Vertical 43
jr 2
s antenna Vertical 47
l r0 antenna Idle
yield
brne r0 0 -2 #wait for dish to idle
j ra

setFilter:
l currFilter selectionDial Setting #filters are loaded onto stack, dial must be 0-12
breq currFilter 0 2 #jump 2 lines IF 0 as it implies no filter
get currFilter db currFilter
yield
j ra

solverRoutine:
move r0 0
move r2 ra #store ra in r2
loadSolver:
l r1 antenna Horizontal
put solver r0 r1
add r0 r0 1
l r1 antenna Vertical
put solver r0 r1
add r0 r0 1
l r1 antenna SignalStrength
put solver r0 r1
add r0 r0 1
jal moveDish
breq r0 9 2 #r0 = 9 means solver is filled, branch
j loadSolver
s solver Setting 1
l r0 solver Setting
yield
breq r0 1 -2 #wait for solver Setting to = 0
get r0 solver 0
s antenna Horizontal r0
get r0 solver 1
s antenna Vertical r0
l r0 antenna Idle
yield
brne r0 0 -2 #wait for dish to idle
j r2 #use stored ra