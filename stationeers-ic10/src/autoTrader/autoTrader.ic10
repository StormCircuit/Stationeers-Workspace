alias solver d0
alias selectionDial d1
alias antenna d2
alias resetButton d3
alias currFilter r4
alias currSigID r7
alias currContTypeID r8
alias count r14
alias prevLine r15
define topOfStack 12 #change this to how many filters you have (if starting at 0, +1)
define minWattsContact 20 #180 and above are medium traders. Large+ is 6,000+
move sp topOfStack

s db Setting 0
s antenna Vertical 45
l r0 antenna Idle
yield
brne r0 1 -2 #wait for dish to idle
s antenna BestContactFilter -1
findContact:
jal moveDish
scanLoop:
jal setFilter #let user change filters only while sweeping
l currContTypeID antenna ContactTypeId #check contactTypeID
bne currContTypeID currFilter continueSweep
l r0 antenna MinimumWattsToContact #check trader tier
blt r0 minWattsContact continueSweep
jal checkIgnoredSignalID #check ignore list
beq r0 1 continueSweep
j attemptContact #if the checks pass, proceed to contact routine
sleep 10
continueSweep:
l r0 antenna Idle
yield
bne r0 1 scanLoop #wait for dish to idle
j findContact

attemptContact:
l r0 antenna SignalStrength
yield
breq r0 -1 -2
l r0 antenna SignalID #set BestContactFilter to our signal ID we found
push r0 #pre-emptively push to ignore list ASAP
s antenna BestContactFilter r0
sleep 10
l r0 antenna MinimumWattsToContact #wait for watts reaching to hit min
l r1 antenna WattsReachingContact
yield
brlt r1 r0 -3
s antenna Activate 1 #watts min reached, activate dish and wait for progress = 100%
l r0 antenna InterrogationProgress
yield
breq r0 -1 -2 #if -1, wait
brne r0 1 -3 #if not 1, wait
jal solverRoutine #run solver routine to get close/at target
s db Setting 1 #trigger waiting flag on housing, set to 0 to continue
waitForReset:
bdns resetButton noButton
l r0 resetButton Setting #give user option to reset with button
brne r0 1 2
s db Setting 0
noButton:
l r0 db Setting
l r0 antenna SignalID
breq r0 -1 2 #signal lost
bne r0 0 waitForReset #wait for housing reset
s antenna BestContactFilter -1
j findContact

setFilter:
l currFilter selectionDial Setting #filters are loaded onto stack, dial must be 0-12
get currFilter db currFilter
j ra
checkIgnoredSignalID:
l r0 antenna SignalID
move sp topOfStack
move currSigID r0
move r0 0
ignoreCheckLoop:
add sp sp 1
pop r0
brne currSigID r0 3 #if currSigID matches an ignored signal, set r0 and branch
move r0 1
j ra
beq r0 0 ra #if stack entry empty (meaning end of stack), branch
j ignoreCheckLoop
moveDish:
l r1 antenna Horizontal #sweep dish, check filter
add r0 r1 20
s antenna Horizontal r0
mul r0 r1 0.7
sin r0 r0
mul r0 r0 22
add r0 r0 55
s antenna Vertical r0
j ra
solverRoutine:
move count 0
move prevLine ra #store ra in prevLine
loadSolver:
l r1 antenna Horizontal
put solver count r1
add count count 1
l r1 antenna Vertical
put solver count r1
add count count 1
l r1 antenna SignalStrength
put solver count r1
add count count 1
jal moveDish
breq count 9 2 #r0 = 9 means solver is filled, branch
j loadSolver
sleep 10
s solver Setting 1
l r0 solver Setting
yield
breq r0 1 -2 #wait for solver Setting to = 0
get r0 solver 0
s antenna Horizontal r0
get r0 solver 1
s antenna Vertical r0
l r0 antenna Idle
yield
brne r0 1 -2 #wait for dish to idle
j prevLine #use stored ra