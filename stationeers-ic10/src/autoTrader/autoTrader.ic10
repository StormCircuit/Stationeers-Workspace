alias solver d0
alias traderFilter d1
alias padSize d4
alias dishMedium d2
alias resetButton d3
alias currFilter r4
alias currDishOffset r5 #how far to move the dish, 21 if scanning/2 if triangulating
alias count r14
alias prevLine r15
define topOfStack 12 #change this to how many filters you have (if starting at 0, +1)
define minWattsContact 100 #180 and above are medium traders. Large+ is 6,000+
move sp topOfStack

s db Setting 0
s dishMedium Vertical 45
l r0 dishMedium Idle
yield
brne r0 1 -2 #wait for dish to idle
findContact:
s dishMedium BestContactFilter -1
move currDishOffset 21
s dishMedium Setting 2000
jal moveDish
jal setFilter #let user change filters only while sweeping
l r0 dishMedium ContactTypeId #check contactTypeID
bne r0 currFilter findContact
s dishMedium Setting 7500
l r0 dishMedium MinimumWattsToContact #check trader tier
yield
breq r0 -1 -2
blt r0 minWattsContact findContact
j attemptContact #if the checks pass, proceed to contact routine

attemptContact:
move currDishOffset 10
l r0 dishMedium SignalID #set BestContactFilter to our signal ID we found
s dishMedium BestContactFilter r0
s dishMedium Activate 0
l r0 dishMedium SignalStrength #wait for green bar
yield
breq r0 -1 -2
l r0 dishMedium MinimumWattsToContact
l r1 dishMedium WattsReachingContact
brgt r1 r0 2 #if we already are reaching it, dont solve
jal solverRoutine
s dishMedium Activate 1
l r0 dishMedium InterrogationProgress
yield
beq r0 -1 findContact #if it's -1 then the signal was lost
brne r0 1 -3 #if not 1, wait
s db Setting 1 #trigger waiting flag on housing, set to 0 to continue
waitForReset:
bdns resetButton noButton
l r0 resetButton Setting #give user option to reset with button
brne r0 1 2
s db Setting 0
noButton:
l r0 dishMedium SignalID
brne r0 -1 2 #signal lost
s db Setting 0
l r0 db Setting
bne r0 0 waitForReset #wait for housing reset
j findContact

setFilter:
l currFilter traderFilter Setting #filters are loaded onto stack, dial must be 0-12
get currFilter db currFilter
j ra
moveDish:
l r1 dishMedium Horizontal #sweep dish, check filter
add r0 r1 currDishOffset
s dishMedium Horizontal r0
mul r0 r1 0.7
sin r0 r0
mul r0 r0 22
add r0 r0 55
s dishMedium Vertical r0
l r0 dishMedium Idle
yield
brne r0 1 -2 #wait for dish to idle
yield
j ra
solverRoutine:
move count 0
move prevLine ra #store ra in prevLine
loadSolver:
l r0 dishMedium SignalID
beq r0 -1 findContact #if signal lost, restart
l r0 dishMedium SignalStrength
yield
breq r0 -1 -4 #wait for dish to idle
l r1 dishMedium Horizontal
put solver count r1
add count count 1
l r1 dishMedium Vertical
put solver count r1
add count count 1
l r1 dishMedium SignalStrength
put solver count r1
add count count 1
breq count 9 3 #r0 = 9 means solver is filled, branch
jal moveDish
j loadSolver
sleep 20
s solver Setting 1
l r0 solver Setting
yield
breq r0 1 -2 #wait for solver Setting to = 0
get r0 solver 0
s dishMedium Horizontal r0
get r0 solver 1
s dishMedium Vertical r0
l r0 dishMedium Idle
yield
brne r0 1 -2 #wait for dish to idle
yield
j prevLine #use stored ra