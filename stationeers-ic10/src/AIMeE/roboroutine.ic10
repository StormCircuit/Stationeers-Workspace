alias robotXmitter d0  #AIMeE controller based on Cows Are Evil modified by Storm
alias recallLever d1   #Lever to trigger recall
alias robomaps d2      #housing which loads stack with map data
alias globalOverrideSwitch d3
alias globalOverrideRecall d4
alias globalOverrideError d5
alias mode r1 
alias charge r2
alias stuck r3
alias maxStack r8
alias WAYX r4
alias WAYZ r5
alias mineables r6 

l maxStack robomaps Setting
l mineables robotXmitter MineablesInVicinity

unloadWait:
move sp 0
s robotXmitter Mode 4
unloadLoop:
yield
l maxStack robomaps Setting #load new max waypoints
ls charge robotXmitter 0 Charge #if charge below 70,000 kw, set error
brgt charge 70000 3
s db Setting 1
j unloadLoop
l r0 recallLever Setting
brne r0 1 3  # if recall is set, reset error state and wait for un-set
s db Setting 0
j unloadLoop
l r0 db Setting #check mining site error, upcoming storm, battery
beq r0 1 unloadLoop
lb r0 1997212478 NextWeatherEventTime Sum
bnez r0 unloadLoop
l mode robotXmitter Mode
beq mode 4 unloadLoop
j goingToWork

goingHomeStart:
add sp sp 1
goingHome:
yield
mod r0 mineables 3  #try to randomize the robots waypoints a tiny bit to stop collisions
sub sp sp 1
blez sp unloadWait
peek WAYZ
sub sp sp 1
peek WAYX
add WAYX WAYX r0
add WAYZ WAYZ r0
s robotXmitter TargetX WAYX
s robotXmitter TargetZ WAYZ
jal travel
j goingHome
goingToWork:
yield
l r0 recallLever Setting #check recall
bnez r0 goingHomeStart
sub r0 maxStack 2
brle sp maxStack 5 #if last waypoint in the stack, randomize coords by 25
mod r0 mineables 50
brgt r0 25 2
mul r0 r0 -1
jr 2
mod r0 mineables 2 #try to randomize the robots waypoints a tiny bit to stop collisions
add sp sp 1 #pop the stack, use peek to improve readability
peek WAYX
add sp sp 1
peek WAYZ
add WAYX WAYX r0
add WAYZ WAYZ r0
s robotXmitter TargetX WAYX
s robotXmitter TargetZ WAYZ
jal travel
bge sp maxStack startMining
j goingToWork

startMining:
add sp sp 1 #add 1 to sp because gohome routine subtracts at the opening
mineOreSearch:
l mineables robotXmitter MineablesInVicinity
blt mineables 0 goingHome
s robotXmitter Mode 3
move stuck 0
mineWait:
yield
add stuck stuck 1 #Mining sites are only viable for so long, check if aimee is wandering
bgt stuck 300 MiningSiteError #~5 minute timer for wandering
l r0 recallLever Setting #check recall, then upcoming storm, then if we're done mining
bnez r0 goingHome
lb r0 1997212478 NextWeatherEventTime Sum
bnez r0 goingHome
l mode robotXmitter Mode
beq mode 3 mineWait #aimee is still mining
bne mode 6 mineOreSearch #must be mode 6 which is when AIMEE is full. Time to go unload
j goingHome

travel:
s robotXmitter Mode 2
wait:
sleep 1
yield
l r0 robotXmitter VelocityMagnitude #perform stuck checking
slt r0 r0 0.2
mul stuck stuck r0
add stuck stuck r0
bgt stuck 20 pathfind
l r0 robotXmitter PositionY # set target Y to current Y to avoid issues
s robotXmitter TargetY r0
l mode robotXmitter Mode
bne mode 0 wait
j ra
pathfind:
s robotXmitter Mode 5 #pathfinding for when something gets in the way
yield
sleep 10
s robotXmitter Mode 2
move stuck 0
j wait
MiningSiteError:
s db Setting 1
j goingHome