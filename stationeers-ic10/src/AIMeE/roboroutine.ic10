alias robotXmitter d0  #=AIMeE controller based on Cows Are Evil modified by Storm=
alias recallLever d1   #flipswitch or lever to trigger recall
alias robomaps d2      #housing which loads stack with map data
alias stuck r2
alias maxStack r3
alias WAYX r4
alias WAYZ r5
alias charge r6
s robotXmitter Mode 0
unload:
move sp 0 #realign sp to prepare for goingToWork
unloadLoop:
l maxStack robomaps Setting #load new max waypoints
jal batteryWeatherCheckRecall
bnez r0 unloadLoop
s robotXmitter Mode 4 #all checks pass, try to unload
sleep 3 #wait a bit to allow robot mode to change back to idle
l r0 robotXmitter Mode
beq r0 4 unloadLoop #if mode = unload, loop
beq r0 0 goingToWork #if mode = idle, go to work
goingHomeStart:
add sp sp 1 #add 1 to sp to reverse stack (accessible when mining/goingToWork)
goingHome:
beq sp 1 unload
sub sp sp 1
peek WAYZ
sub sp sp 1
peek WAYX
brgt sp 1 5 #if first waypoint in stack, randomize coords by +4 or -4
rand r0
mul r0 r0 8
sub r0 r0 4
jr 2 #use random number for r0, skip using -1
move r0 -1 #sub 1 from WAYX/Z to move coords to the left and avoid collisions
add WAYX WAYX r0
add WAYZ WAYZ r0
s robotXmitter TargetX WAYX
s robotXmitter TargetZ WAYZ
jal travel
j goingHome
goingToWork:
jal batteryWeatherCheckRecall
bne r0 0 goingHomeStart
add sp sp 1 #pop the stack, use peek to improve readability
peek WAYX
add sp sp 1
peek WAYZ
brlt sp maxStack 5 #if last waypoint in stack, randomize coords by up to +150 or -150
rand r0
mul r0 r0 300
sub r0 r0 150
jr 2 #skip next to use randomized r0 value
move r0 1
add WAYX WAYX r0
add WAYZ WAYZ r0
s robotXmitter TargetX WAYX
s robotXmitter TargetZ WAYZ
jal travel
bge sp maxStack mineOreSearch
j goingToWork
mineOreSearch:
l r0 robotXmitter MineablesInVicinity
ble r0 10 goingHomeStart
s robotXmitter Mode 3
move stuck 0
mineWait:
add stuck stuck 1 #Mining sites are only viable for so long, check if aimee is wandering
bgt stuck 300 MiningSiteError #~5 minute timer for wandering
yield
l r0 robotXmitter Mode
beq r0 3 mineWait #aimee is still mining
beq r0 6 goingHomeStart #mode 6 which is when AIMEE is full. Time to go unload
jal batteryWeatherCheckRecall
bne r0 0 goingHomeStart
j mineOreSearch
travel:
s robotXmitter Mode 2
move r1 ra #store ra
sleep 1 #give aimee time to roll
wait:
yield
jal batteryWeatherCheckRecall
l r0 robotXmitter VelocityMagnitude #perform stuck checking
slt r0 r0 0.2
mul stuck stuck r0
add stuck stuck r0
bge stuck 61 pathfind
l r0 robotXmitter PositionY # set target Y to current Y to avoid issues
s robotXmitter TargetY r0
l r0 robotXmitter Mode
bne r0 0 wait
j r1 #use stored ra
pathfind:
s robotXmitter Mode 5 #pathfinding for when something gets in the way
sleep 10
s robotXmitter Mode 2
move stuck 0
j wait
MiningSiteError:
s db Setting 1
j goingHomeStart
batteryWeatherCheckRecall:
ls r0 robotXmitter 0 Charge
slt r0 r0 70000
breq r0 1 7 #if charge is  below 70000, go straight to error
lb r0 1997212478 NextWeatherEventTime Sum
sgt r0 r0 0 #normalize r1
brgtz r0 5 #if weather, skip past all checks, do not set error
l r0 recallLever Setting  #check recall
breqz r0 4 #if lever off, do not do anything to error
move r0 0 #if lever on, reset error and set r0 = 1
s db Setting r0
move r0 1
j ra #if any checks pass, r0 = 1 else r0 = 0.