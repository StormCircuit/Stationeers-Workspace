alias robotXmitter d0  #=Based on Cows Are Evil, modified EXTENSIVELY by StormCircuit=
alias recallLever d1   #Anything with a Setting. 1 = recall/reset error
s robotXmitter Mode 0 # stuck is r2 r3 is topOfStack, r4 is WAYX, #r5 is WAYZ
s db Setting 0
unload:
move sp 0 #realign sp to prepare for travel to unload point
unloadLoop:
get r3 db 511 #load new max waypoints at unload position
jal batteryWeatherCheckRecall
bnez r0 unloadLoop
s robotXmitter Mode 4
sleep 1
l r0 robotXmitter Mode
beq r0 4 unloadLoop
beq r0 0 goingToWork
goingHomeStart:
add sp sp 1 #add 1 to sp to reverse stack (accessible when mining/goingToWork)
ble sp 1 unload #==BEGIN GOING HOME LOOP==#
sub sp sp 1
peek r5
sub sp sp 1
peek r4
brgt sp 1 5 #if first waypoint in stack, randomize coords by +2.5 or -2.5
rand r0
mul r0 r0 5
sub r0 r0 2.5
jr 2 #use random number for r0, skip using -1
move r0 -1 #sub 1 from r4/Z to move coords to the left and avoid collisions
add r4 r4 r0
add r5 r5 r0
s robotXmitter TargetX r4
s robotXmitter TargetZ r5
jal travel
jr -16 #==END GOING HOME LOOP==
goingToWork:
jal batteryWeatherCheckRecall
bne r0 0 goingHomeStart
add sp sp 1
peek r4
add sp sp 1
peek r5
brlt sp r3 7 #if last waypoint in stack, randomize coords by given memory
rand r0
get r1 db 510 # mining range is stored at stack address 510 by robomaps
mul r0 r0 r1
div r1 r1 2
sub r0 r0 r1
jr 2 #skip next to use randomized r0 value
move r0 1
add r4 r4 r0
add r5 r5 r0
s robotXmitter TargetX r4
s robotXmitter TargetZ r5
jal travel
bge sp r3 mineOreSearch
j goingToWork
mineOreSearch:
s robotXmitter Mode 3
move r2 0
jal batteryWeatherCheckRecall  #==BEGIN MINING WAIT LOOP==#
bne r0 0 goingHomeStart
add r2 r2 1
get r0 db 508 #timeout max stored at 507, if we go over we potentialy trigger a error
brlt r2 r0 8
l r0 robotXmitter MineablesInVicinity
put db 506 r0 #put MineablesInVicinity in 506 for external reading
get r1 db 509 #if theres less ore than the number at this stack, error
brle r0 r1 3
sub sp sp 2 #move stack back 2 to previous waypoint, find new spot
j goingToWork
j MiningSiteError #if mineables <= SP 509 AND wandering, error
yield
l r0 robotXmitter Mode
breq r0 3 -14 #==END MINING WAIT LOOP==
beq r0 6 goingHomeStart
j mineOreSearch
travel:
s robotXmitter Mode 2
move r1 ra #store ra
l r0 robotXmitter PositionY #==BEGIN TRAVEL WAIT LOOP ON THIS LINE==
s robotXmitter TargetY r0  #set target Y to current Y to avoid path issues
jal batteryWeatherCheckRecall
breqz r0 5
brlt r1 goingToWork 4 # if from gohome, skip next
move r1 goingHomeStart
sub sp sp 2 #store gohome in r1, get last waypoint
s robotXmitter Mode 0 #set robot to idle to trigger instant reverse
l r0 robotXmitter VelocityMagnitude
slt r0 r0 0.2
mul r2 r2 r0
add r2 r2 r0
bge r2 70 pathfind  #perform r2 checking, give aimee a chance to bounce
yield
l r0 robotXmitter Mode
brne r0 0 -15 #==END TRAVEL WAIT LOOP ON THIS LINE==
put db 503 sp #store last visited sp for external use
j r1 #use stored ra, if error/recall is on then this is goingHome
pathfind:
s robotXmitter Mode 5
sleep 10
s robotXmitter Mode 2
move r2 0
jr -23 #==LOOP BACK TO BEGIN TRAVEL WAIT LOOP==
MiningSiteError:
s db Setting 1
move r0 1
j goingHomeStart
batteryWeatherCheckRecall:
l r0 robotXmitter PositionX
put db 500 r0
l r0 robotXmitter PositionZ
put db 501 r0
ls r0 robotXmitter 0 Damage
put db 504 r0
brnez r0 4
ls r0 robotXmitter 0 Charge
put db 505 r0
slt r0 r0 70000
breq r0 1 7 #go straight to error
lb r0 1997212478 NextWeatherEventTime Sum
brnez r0 7 #if weather, skip Setting and lever
l r0 recallLever Setting
brne r0 1 5 #if lever on, reset error and set r0 = 1
s db Setting 0
jr 3
move r0 1
s db Setting r0
j ra #if any checks pass, r0 = 1 else r0 = 0.