alias robotXmitter d0  #=Based on Cows Are Evil, modified EXTENSIVELY by StormCircuit=
alias recallLever d1   #Anything with a Setting. 1 = recall/reset error
s robotXmitter Mode 0  #r2=stuck,r3=topOfStack,r4=WAYX,r5=WAYZ,r6=Error,r7=MiningError
unload:                #r15=ra
move sp 0 #realign sp
get r3 db 511 #load new max waypoints at unload position
jal batteryWeatherCheckRecall
brnez r6 -2 #skip back to unload loop
s robotXmitter Mode 4
yield  #=BEGIN UNLOAD LOOP=
l r0 robotXmitter Mode
breq r0 4 -2  #=END UNLOAD LOOP=
beq r0 0 goingToWork
goingHomeStart:
add sp sp 1 #accessible if mining/goingToWork
jal batteryWeatherCheckRecall #update stack
ble sp 1 unload #==BEGIN GOING HOME LOOP==#
sub sp sp 1
peek r5
sub sp sp 1
peek r4
brgt sp 1 5 #collision avoidance
rand r0
mul r0 r0 5
sub r0 r0 2.5
jr 2 #use random number for r0, skip using -1
move r0 -1
add r4 r4 r0
add r5 r5 r0
s robotXmitter TargetX r4
s robotXmitter TargetZ r5
jal travel
jr -17 #==END GOING HOME LOOP==
goingToWork:
jal batteryWeatherCheckRecall
bne r6 0 goingHomeStart
add sp sp 1
peek r4
add sp sp 1
peek r5
brlt sp r3 7 #if last waypoint in stack, randomize coords by given memory
rand r0
get r1 db 510 # mining range is stored at stack address 510 by robomaps
mul r0 r0 r1
div r1 r1 2
sub r0 r0 r1
jr 2
move r0 1
add r4 r4 r0
add r5 r5 r0
s robotXmitter TargetX r4
s robotXmitter TargetZ r5
jal travel
bge sp r3 mineOreSearch
j goingToWork
mineOreSearch:
s robotXmitter Mode 3
move r2 0
jal batteryWeatherCheckRecall  #==BEGIN MINING WAIT LOOP==#
bne r6 0 goingHomeStart
add r2 r2 1 #add to stuck
get r0 db 508 #timeout max stored at 508, if we go over we potentialy trigger a error
brlt r2 r0 8
get r1 db 509 #if theres less ore than the number at this stack, error
brle r0 r1 3
sub sp sp 2
j goingToWork
s db Setting 1 #mining error triggered
move r7 1
j goingHomeStart #if mineables <= SP 509 AND wandering, error
yield
l r0 robotXmitter Mode
breq r0 3 -14 #==END MINING WAIT LOOP==
beq r0 6 goingHomeStart
j mineOreSearch
travel:
s robotXmitter Mode 2
move r15 ra #store ra
l r0 robotXmitter PositionY #==BEGIN TRAVEL WAIT LOOP ON THIS LINE==
s robotXmitter TargetY r0
jal batteryWeatherCheckRecall
breqz r6 5
brlt r15 goingToWork 4 # if from gohome, skip next
move r15 goingHomeStart
sub sp sp 2 #store gohome in r1, get last waypoint
s robotXmitter Mode 0 #set robot to idle to trigger instant reverse
l r0 robotXmitter VelocityMagnitude
slt r0 r0 0.2
mul r2 r2 r0
add r2 r2 r0
bge r2 70 pathfind  #perform r2 checking, give aimee a chance to bounce
yield
l r0 robotXmitter Mode
brne r0 0 -15 #==END TRAVEL WAIT LOOP ON THIS LINE==
j r15 #use stored ra, if error/recall is on then this is goingHome
pathfind:
s robotXmitter Mode 5
sleep 10
s robotXmitter Mode 2
move r2 0
jr -22 #==LOOP BACK TO BEGIN TRAVEL WAIT LOOP==
batteryWeatherCheckRecall:
put db 507 sp #store sp for external read
put db 506 ra #store ra for external read
put db 504 r2 #store stuck value for external read
put db 502 r5 #WAYZ, meaning TargetZ
put db 501 r4 #WAYX, meaning TargetX
ls r0 robotXmitter 0 Charge #check if below SP 505
get r1 db 505 #check charge value against SP 505 in watts
slt r6 r0 r1
ls r0 robotXmitter 0 Damage #check BATTERY damage
get r1 db 503
sgt r0 r0 r1 #check BATTERY damage value against SP 503 in PERCENTAGE
or r6 r6 r0 #r6 = 1 if either are bad
brnez r6 9 #if damage/charge check = 1 then skip rest, SET error SIGNAL/Setting
or r6 r6 r7 #if any above OR mining error, recall
l r0 recallLever Setting
seq r0 r0 1 #normalize the recall signal, MUST = 1
or r6 r6 r0 #if lever OR damage/charge = 1 then r6 = 1
breqz r0 4 #if lever set, error SIGNAL/Setting must clear, MiningError must clear
s db Setting 0
move r7 0
jr 2
s db Setting r6 #this means you can clear a battery or damage error by just fixing it
lb r0 1997212478 NextWeatherEventTime Sum
or r6 r6 r0 #If damage/charge/Mining Error/recall signal/ then r6 = 1
j ra