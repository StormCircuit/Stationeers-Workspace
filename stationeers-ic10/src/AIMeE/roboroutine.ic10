#AIMeE base controller based on Cows Are Evil's scripts
#Modified by Storm to progress up and down a stack loaded set of waypoints for pathing

#Short user instructions:
#1. Set GPS locations using GPS tablet and write down coordinates
#2. Put them into stack like so: PUSH X1 PUSH Y1 PUSH X2 PUSH Y2 until all added
#NOTE: X1/Y1 is the unloading chute/dock location, last pair are the mining location

alias robot d0         #Logic Transmitter
alias lever d1         #Lever to trigger recall
alias robomaps d2      #housing which loads stack with map data

alias mode r1
alias charge r2
alias stuck r3
alias maxStack r8
alias WAYX r4
alias WAYZ r5
alias MINEX r6
alias MINEZ r7

define NONE 0
define KILL 1
define MOVE 2
define MINE 3
define UNLOAD 4
define PATH 5
define FULL 6

l maxStack robomaps Setting
move sp 0
j unloadLoop

goingHome:
yield
beqz sp unloadWait
pop WAYX
pop WAYZ
s robot TargetX WAYX
s robot TargetZ WAYZ
s db Setting WAYX
jal travel
j goingHome

unloadWait:
s robot Mode UNLOAD
unloadLoop:
yield
#check recall
l r0 lever Setting
bnez r0 unloadLoop
#check battery
ls charge robot 0 Charge
blt charge 70000 unloadLoop
l mode robot Mode
beq mode UNLOAD unloadLoop
j goingToWork

#unload done, travel to mining location
goingToWork:
yield
l maxStack robomaps Setting
add sp sp 1
peek WAYX
add sp sp 1
peek WAYZ
s robot TargetX WAYX
s robot TargetZ WAYZ
jal travel
bge sp maxStack startMining
j goingToWork

#mine until full
startMining:
l r0 lever Setting
bnez r0 goingHome
#check for mineables
l r0 robot MineablesInVicinity
beqz r0 goingHome
s robot Mode MINE
mineWait:
yield
#check recall
l r0 lever Setting
l mode robot Mode
bnez r0 goingHome
beq mode MINE mineWait
bne mode FULL startMining
j goingToWork

travel: 
s robot Mode MOVE
wait:
sleep 10
yield
#check recall
l r0 lever Setting
bnez r0 goingHome
l r0 robot VelocityMagnitude
slt r0 r0 0.2
mul stuck stuck r0
add stuck stuck r0
bgt stuck 20 pathfind
l r0 robot PositionY
s robot TargetY r0
l mode robot Mode
bne mode 0 wait
j ra

pathfind:
s robot Mode PATH
yield
sleep 10
s robot Mode MOVE
move stuck 0
j wait